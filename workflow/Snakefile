# import required libraries
from snakemake.utils import min_version
from pathlib import Path


##### set minimum snakemake version #####
min_version("8.4.1")


containerized: "docker://ghcr.io/ht-diva/pqtl_pipeline_finemap:ee49bec"


# read the configuration file
configfile: "conf/config_finemap.yaml"


include: "rules/common.smk"


rule all:
    input:
        expand(ws_path("logs/cojo/{seqid}.log"), seqid=analytes.seqid),


rule break_locus:
    input:
        gwas=get_sumstats,
    output:
        loci=ws_path("break/{seqid}_loci.csv"),
    conda:
        "envs/r_environment.yml"
    params:
        codes=config.get("path_code"),
        study_id="{seqid}",
        outdir=ws_path("break/{seqid}"),
        p1=config.get("thresholds").get("p1"),
        p2=config.get("thresholds").get("p2"),
        hole=config.get("thresholds").get("hole"),
        p_label=config.get("labels").get("p_label"),
        chr_label=config.get("labels").get("chr_label"),
        pos_label=config.get("labels").get("pos_label"),
    log:
        ws_path("logs/break/{seqid}.log"),
    resources:
        runtime=lambda wc, attempt: attempt * 30,
    shell:
        """
        Rscript=`ls /conda-envs/*/bin/Rscript`;
        $Rscript workflow/scripts/s03_locus_breaker.R \
            --pipeline_path {params.codes} \
            --input {input.gwas} \
            --study_id '{params.study_id}' \
            --p_thresh1 {params.p1} \
            --p_thresh2 {params.p2} \
            --hole      {params.hole} \
            --outdir    {params.outdir} \
            --p_label   {params.p_label} \
            --chr_label {params.chr_label} \
            --pos_label {params.pos_label} 2> {log}
        """


rule run_cojo:
    input:
        gwas=get_sumstats,
        loci=ws_path("break/{seqid}_loci.csv"),
    output:
        odir=ws_path("cojo/{seqid}/"),
        log=ws_path("logs/cojo/{seqid}.log"),
    conda:
        "envs/r_environment.yml"
    params:
        codes=config.get("path_code"),
        geno=config.get("path_geno"),
        ofile=ws_path("cojo/{seqid}"),
        ppp=config.get("thresholds").get("ppp"),
    log:
        ws_path("logs/cojo/{seqid}.log"),
    resources:
        mem_mb=8000,
        runtime=60,
    shell:
        """
        Rscript=`ls /conda-envs/*/bin/Rscript`;
        INPUT_FILE={input.loci};

        # read loci and loop over each locus
        while IFS=, read -r col1 col2 col3 col4_onwards; do
            # Assign the values of the first three columns to variables
            chr=$col1
            beg=$col2
            end=$col3

            # Skip the first line then run the script
            if [[ "$chr" != "chr" ]]; then
                Rscript scripts/s04_cojo_finemapping.R  \
                --pipeline_path   {params.codes}  \
                --dataset_aligned {input.gwas}  \
                --study_id {params.ofile}  \
                --chr   "$chr"  \
                --start "$beg"  \
                --end   "$end"  \
                --bfile {params.geno} \
                --cs_thresh {params.ppp}  \
                --outdir {output.odir} \
                --phenotype_id full  \
                --nf_hcoloc_v no_nf  2>> {log}
            fi
        done < "$INPUT_FILE"
        """
