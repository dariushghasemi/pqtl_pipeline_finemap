# import required libraries
from snakemake.utils import min_version


##### set minimum snakemake version #####
min_version("8.4.1")


containerized: "docker://ghcr.io/ht-diva/pqtl_pipeline_finemap:ee49bec"


# read the configuration file
configfile: "conf/config_finemap.yaml"


include: "rules/common.smk"


rule all:
    input:
        expand(ws_path("cojo/{seqid}/sentinel.txt"), seqid=analytes.seqid),
        #expand(ws_path("break/{seqid}_loci.csv"), seqid=analytes.seqid)
        ws_path("break/collected_loci_excluding_mhc.csv")



rule break_locus:
    input:
        gwas=get_sumstats,
    output:
        loci=ws_path("break/{seqid}_loci.csv"),
    conda:
        "envs/r_environment.yml"
    params:
        codes=config.get("path_code"),
        study_id="{seqid}",
        outdir=ws_path("break/{seqid}"),
        p1=config.get("thresholds").get("p1"),
        p2=config.get("thresholds").get("p2"),
        hole=config.get("thresholds").get("hole"),
        p_label=config.get("labels").get("p_label"),
        chr_label=config.get("labels").get("chr_label"),
        pos_label=config.get("labels").get("pos_label"),
    log:
        ws_path("logs/break/{seqid}.log"),
    resources:
        runtime=lambda wc, attempt: attempt * 30,
    shell:
        """
        Rscript=`ls /conda-envs/*/bin/Rscript`;
        $Rscript workflow/scripts/s03_locus_breaker.R \
            --pipeline_path {params.codes} \
            --input {input.gwas} \
            --study_id '{params.study_id}' \
            --p_thresh1 {params.p1} \
            --p_thresh2 {params.p2} \
            --hole      {params.hole} \
            --outdir    {params.outdir} \
            --p_label   {params.p_label} \
            --chr_label {params.chr_label} \
            --pos_label {params.pos_label} > {log}
        """


rule collect_loci:
    input:
        expand(ws_path("break/{seqid}_loci.csv"), seqid=analytes.seqid),
    output:
        ofile=ws_path("break/collected_loci_excluding_mhc.csv"),
    conda:
        "envs/r_environment.yml"
    resources:
        runtime=lambda wc, attempt: attempt * 30,
    script:
        "scripts/s02_collect_loci.R"



rule run_cojo:
    input:
        gwas=get_sumstats,
        loci=ws_path("break/{seqid}_loci.csv"),
    output:
        sentinel=ws_path("cojo/{seqid}/sentinel.txt"),
        log=ws_path("logs/cojo/{seqid}.log"),
    conda:
        "envs/r_environment.yml"
    params:
        codes=config.get("path_code"),
        geno=config.get("path_geno"),
        ofile=ws_path("cojo/{seqid}"),
        mapping=config.get("path_mapping"),
        ppp=config.get("thresholds").get("ppp"),
        p3=config.get("thresholds").get("p3"),
        p4=config.get("thresholds").get("p4"),
        maf=config.get("thresholds").get("maf"),
        p_label=config.get("labels").get("p_label"),
        chr_label=config.get("labels").get("chr_label"),
        pos_label=config.get("labels").get("pos_label"),
        snpid_label=config.get("labels").get("snpid_label"),
        key_label=config.get("labels").get("key_label"),
        ea_label=config.get("labels").get("ea_label"),
        oa_label=config.get("labels").get("oa_label"),
        eaf_label=config.get("labels").get("eaf_label"),
        se_label=config.get("labels").get("se_label"),
        beta_label=config.get("labels").get("beta_label"),
        n_label=config.get("labels").get("n_label"),
    log:
        ws_path("logs/cojo/{seqid}.log"),
    resources:
        runtime=lambda wc, attempt: attempt * 60,
        mem_mb=lambda wc, attempt: 8000 + attempt * 2048
    shell:
        """
        Rscript=`ls /conda-envs/*/bin/Rscript`;
        INPUT_FILE={input.loci};

        # read loci and loop over each locus
        while IFS=, read -r col1 col2 col3 col4_onwards; do
            # Assign the values of the first three columns to variables
            chr=$col1
            beg=$col2
            end=$col3

            # Check if chr is empty and exit the loop if it is
            if [[ -z "$beg" ]]; then
                break
            fi

            # Skip the first line then run the script
            if [[ "$chr" != "chr" ]]; then
                Rscript workflow/scripts/s04_cojo_finemapping.R  \
                --pipeline_path   {params.codes}  \
                --dataset_gwas {input.gwas}  \
                --mapping  {params.mapping}  \
                --study_id {params.ofile}  \
                --chr   "$chr"  \
                --start "$beg"  \
                --end   "$end"  \
                --bfile {params.geno} \
                --cs_thresh {params.ppp}  \
                --p_thresh3 {params.p3}  \
                --p_thresh4 {params.p4}  \
                --maf       {params.maf}  \
                --outdir {params.ofile} \
                --phenotype_id full  \
                --plink2_mem {resources.mem_mb}  \
                --plink2_threads {threads} \
                --p_label   {params.p_label} \
                --chr_label {params.chr_label} \
                --pos_label {params.pos_label} \
                --snpid_label {params.snpid_label} \
                --ea_label {params.ea_label} \
                --oa_label {params.oa_label} \
                --eaf_label {params.eaf_label} \
                --se_label {params.se_label} \
                --beta_label {params.beta_label} \
                --n_label {params.n_label} \
                --key_label {params.key_label} >> {log}
            fi
        done < "$INPUT_FILE"
        touch {output.sentinel}
        """
